plugins {
	id 'org.springframework.boot' version '3.0.4'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'java'
	id 'jacoco'
	id 'org.owasp.dependencycheck' version '7.4.4'
	id 'com.github.spotbugs' version '5.0.13'
}

group = 'com.prm-gp-registrations-mi'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	all {
		exclude group: "org.springframework.boot", module: 'spring-boot-starter-logging'
	}
}

repositories {
	mavenCentral()
}

//Force sub-dependency versions
ext['spring-webmvc.version'] = '6.0.7'  // remove once org.springdoc:springdoc-openapi-ui:1.6.16 or above is used

//Without this task two jars are built, the additional "-plain.jar" is not needed
// for more details refer to: https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/#packaging-executable.and-plain-archives
jar {
	enabled = false
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.springframework:spring-core:6.0.7'	// set 6.0.7 to solve vulnerability CVE-2023-20861
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
    implementation 'software.amazon.awssdk:sqs:2.20.14'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'com.github.spotbugs:spotbugs-annotations:4.7.3'
	implementation 'com.googlecode.json-simple:json-simple:1.1.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
}

tasks.withType(Test) {
	useJUnitPlatform()
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

jacocoTestCoverageVerification {
	dependsOn jacocoTestReport

	violationRules {
		rule {
			limit {
				minimum = 0.8
			}
		}
	}
}

dependencyCheck {
	failBuildOnCVSS = 7
	suppressionFile = './dependency-checks-suppression.xml'
}

spotbugsMain {
	reports {
		html {
			enabled = true
			destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

spotbugsTest {
	reports {
		html {
			enabled = true
			destination = file("$buildDir/reports/spotbugs/test/spotbugs.html")
			stylesheet = 'fancy-hist.xsl'
		}
	}
}
